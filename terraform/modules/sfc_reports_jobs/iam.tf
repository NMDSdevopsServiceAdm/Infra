data "aws_iam_policy_document" "assume_role" {
  statement {
    effect = "Allow"

    principals {
      type        = "Service"
      identifiers = ["lambda.amazonaws.com"]
    }

    actions = ["sts:AssumeRole"]
  }
}

resource "aws_iam_role" "lambda_role" {
  name               = "LambdaServiceRole"
  assume_role_policy = data.aws_iam_policy_document.assume_role.json
}

data "aws_iam_policy_document" "lambda_analysis_file_logging_policy" {
  statement {
    effect = "Allow"

    actions = [
      "logs:CreateLogGroup",
      "logs:CreateLogStream",
      "logs:PutLogEvents",
    ]

    resources = ["arn:aws:logs:*:*:*"]
  }
}

data "aws_iam_policy_document" "lambda_analysis_file_execution_policy" {
 statement {
    effect = "Allow"

    actions = [
      "ec2:CreateNetworkInterface",
      "ec2:DescribeNetworkInterfaces",
      "ec2:DeleteNetworkInterface"
    ]

    resources = ["*"]
  }
}
 
resource "aws_iam_policy" "lambda_analysis_file_logging_policy" {
  name        = "analysis_file_lambda_logging"
  path        = "/"
  description = "IAM policy for logging from a lambda"
  policy      = data.aws_iam_policy_document.lambda_analysis_file_logging_policy.json
}

resource "aws_iam_policy" "lambda_analysis_file_execution_policy" {
  name        = "analysis_file_lambda_execution"
  description = "IAM policy for executing the lambda"
  policy      = data.aws_iam_policy_document.lambda_analysis_file_execution_policy.json
}

resource "aws_iam_role_policy_attachment" "lambda_analysis_file_logging_policy_attachment" {
  role       = aws_iam_role.lambda_role.id
  policy_arn = aws_iam_policy.lambda_analysis_file_logging_policy.arn
}

resource "aws_iam_role_policy_attachment" "lambda_analysis_file_execution_policy_attachment" {
  role       = aws_iam_role.lambda_role.id
  policy_arn = aws_iam_policy.lambda_analysis_file_execution_policy.arn
}